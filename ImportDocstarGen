Sub ImportDocstar()
    Dim csvFilePath As String
    Dim qt As QueryTable
    Dim ws As Worksheet
    Dim colName As String
    Dim colIndex As Long
    Dim colNamesToDelete As Collection
    Dim DocstarColumns As Variant
    Dim tbl As ListObject
    Dim nm As String
    Dim dcstr_lang As String
    dcstr_lang = ThisWorkbook.Sheets("Statement").ComboBox1.Value
    
    ' CHECK DOCSTAR SELECTED LANGUAGE
    If dcstr_lang = "ENGLISH" Then
        DocstarColumns = Array("InvoiceNum", "InvoiceAmt", "PONum", "Workflow Step")
    ElseIf dcstr_lang = "FRANÇAIS" Then
        DocstarColumns = Array("InvoiceNum", "InvoiceAmt", "PONum", "Ã‰tape du flux de travail")
    Else
        ' Handle unexpected value
        MsgBox "Please select a language for Docstar.", vbCritical, "Error"
        Exit Sub
    End If

    nm = "TABLE NAME" ' ***************** ENTER TABLE NAME
    Set ws = ThisWorkbook.Sheets("SHEET") ' ************** ENTER SHEET NAME

    ' IMPORT CSV
    csvFilePath = GetUserSelectedFile("Please select Docstar data file:")
    Set qt = ws.QueryTables.Add(Connection:="TEXT;" & csvFilePath, Destination:=ws.range("A1"))
    
    With qt
        .TextFileParseType = xlDelimited
        .TextFileCommaDelimiter = True
        .TextFileColumnDataTypes = Array(1)
        .Refresh BackgroundQuery:=False
    End With
    qt.Delete
    
    ' CREATE TABLE
    Set tbl = ws.ListObjects.Add(SourceType:=xlSrcRange, _
                                 Source:=ws.range("A1").CurrentRegion, _
                                 xlListObjectHasHeaders:=xlYes)
    
    tbl.Name = nm
     ' REARRANGE COLUMNS
    For i = LBound(DocstarColumns) To UBound(DocstarColumns)
        colName = DocstarColumns(i)
        colIndex = tbl.ListColumns(colName).Index
        
        ' Move the column to the desired position
        If colIndex <> i + 1 Then
            tbl.ListColumns(colName).Range.Cut
            tbl.ListColumns(i + 1).Range.Insert Shift:=xlToRight
        End If
    Next i
    
    ' DELETE UNNECESSARY COLUMNS
    Set colNamesToDelete = New Collection
    
    For Each col In tbl.ListColumns
        colName = col.Name
        If IsError(Application.Match(colName, DocstarColumns, 0)) Then
            colNamesToDelete.Add colName
        End If
    Next col
    For i = colNamesToDelete.Count To 1 Step -1
        tbl.ListColumns(colNamesToDelete(i)).Delete
    Next i
    tbl.Range.Columns.AutoFit
    
    ' CHANGE INVOICE# INTO NUMBER TYPE WITH TEXT TO COLUMNS
    ws.range("A:A").TextToColumns Destination:=range("A:A"), _
        DataType:=xlDelimited, _
        TextQualifier:=xlDoubleQuote, _
        ConsecutiveDelimiter:=False, _
        Tab:=False, _
        Semicolon:=False, _
        Comma:=False, _
        Space:=False, _
        Other:=False, _
        FieldInfo:=Array(1, 1), _
        TrailingMinusNumbers:=True
    
    Sheets("Statement").range("F2").FormulaR1C1 = "=VLOOKUP(RC[-5]," & tbl.name & ", 4, FALSE)"
    
    Sheets("Statement").range("G2").FormulaR1C1 = "=IF(RC[-3]=VLOOKUP(RC[-6], " & tbl.name & ",2,FALSE),""Y"",""N"")"
End Sub

