Sub ImportWorkday()
    Dim xlsxFilePath As String
    Dim csvFilePath As String
    Dim wb As Workbook
    Dim qt As QueryTable
    Dim ws As Worksheet

    ' CONVERT TO CSV
    xlsxFilePath = GetUserSelectedFile("Please select Workday data file:")
    csvFilePath = Left(xlsxFilePath, Len(xlsxFilePath) - 4) & "csv"

    Set wb = Workbooks.Open(xlsxFilePath)
    wb.Sheets(1).SaveAs Filename:=csvFilePath, FileFormat:=xlCSV
    wb.Close SaveChanges:=False
    
    ' IMPORT DATA
    Set ws = ThisWorkbook.Sheets("Workday")
    Set qt = ws.QueryTables.Add(Connection:="TEXT;" & csvFilePath, Destination:=ws.range("A1"))
    
    With qt
        .TextFileParseType = xlDelimited
        .TextFileCommaDelimiter = True
        .TextFileColumnDataTypes = Array(1) ' Treat all columns as text
        .Refresh BackgroundQuery:=False
    End With
    qt.Delete
    
    ' CONVERT DATA INTO TABLE
    Dim tbl As ListObject
    Set tbl = ws.ListObjects.Add(SourceType:=xlSrcRange, _
                                 Source:=ws.range("A1").CurrentRegion, _
                                 xlListObjectHasHeaders:=xlYes)
    tbl.Name = "WD"
    tbl.Range.Columns.AutoFit

    ' DELETE UNNECESSARY COLUMNS (DIFFERENT FOR EACH DATABASE!)
    ws.Columns("A:E").Delete Shift:=xlToLeft
    
    ' CHANGE INVOICE NO INTO NUMBER TYPE WITH TEXT TO COLUMNS
    ws.range("A:A").TextToColumns Destination:=range("A:A"), _
        DataType:=xlDelimited, _
        TextQualifier:=xlDoubleQuote, _
        ConsecutiveDelimiter:=False, _
        Tab:=False, _
        Semicolon:=False, _
        Comma:=False, _
        Space:=False, _
        Other:=False, _
        FieldInfo:=Array(1, 1), _
        TrailingMinusNumbers:=True
    
    Sheets("Statement").range("E2").FormulaR1C1 = "=VLOOKUP(RC[-4],WD,7,FALSE)"
End Sub
